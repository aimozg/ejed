buildscript {
    ext.kotlin_version = '1.3.11'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'kotlin-platform-jvm'
version '0.1.8'

repositories {
    mavenCentral()
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

sourceSets {
    main {
        kotlin {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        kotlin {
            srcDirs = ['test']
        }
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ej.editor.EditorApp',
                'Class-Path': project.name+'-libs.jar'
        )
    }
}

task libsJar(type: Jar) {
    classifier = 'libs'
    group = 'build'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/MANIFEST.MF"
    }
}
task fatJar(type: Jar) {
    manifest.from jar.manifest
    group = 'build'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/MANIFEST.MF"
    }
    with jar
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.controlsfx:controlsfx:8.40.14"
    compile "no.tornado:tornadofx:1.7.16"
    compile "org.funktionale:funktionale-either:1.2"
    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.9.1'
//    compile 'com.uchuhimo:kotlinx-bimap:1.0'
    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}
